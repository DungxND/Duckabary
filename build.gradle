plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.0-rc-1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0-RC2'
}


group 'io.vn.dungxnd.duckabary'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.11.3'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


application {
    mainModule = 'io.vn.dungxnd.duckabary'
    mainClass = 'io.vn.dungxnd.duckabary.AppCommandline'

    // use of J23 garbage collector
    applicationDefaultJvmArgs = ['-XX:+UseZGC', '-XX:+ZGenerational']
}

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
    modularity.inferModulePath = false
}


javafx {
    version = '24-ea+15'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('eu.hansolo:tilesfx:21.0.9') {
        exclude(group: 'org.openjfx')
    }
    implementation 'lk.vivoxalabs.customstage:CustomStage:1.3.2'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.jetbrains:annotations:24.0.0'

    // Google Book API
    implementation 'com.google.apis:google-api-services-books:v1-rev20240214-2.0.0'

    // Tester
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // To use .env file
    implementation 'io.github.cdimascio:dotenv-java:3.0.2'

    // To work with DB
    implementation 'org.xerial:sqlite-jdbc:3.47.0.0'
    implementation 'com.zaxxer:HikariCP:6.2.0'

//    implementation 'tech.turso.libsql:libsql:0.1.0'

    // Kotlin to run DB things
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0-RC2'

    // Encrypt password
    implementation 'com.password4j:password4j:1.8.2'

    // Logging?
    implementation 'org.slf4j:slf4j-api:2.1.0-alpha1'
    implementation 'org.slf4j:slf4j-simple:2.1.0-alpha1'


    // JavaFX
    implementation 'org.openjfx:javafx-controls:24-ea+15'
    implementation 'org.openjfx:javafx-fxml:24-ea+15'
}

tasks.jar {
    manifest {
        attributes(
                'Main-Class': 'io.vn.dungxnd.duckabary.MainApplication',
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.layout.buildDirectory.file("distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Duckabary'
    }
    jpackage {
        installerType = 'msi'
        installerName = 'Duckabary Installer'
    }
}

jlinkZip {
    group = 'distribution'
}

