plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "co.uzzu.dotenv.gradle" version "4.0.0"
}

group 'io.vn.dungxnd.duckabary'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.11.0'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.vn.dungxnd.duckabary'
    mainClass = 'io.vn.dungxnd.duckabary.HelloApplication'
}

java {
    modularity.inferModulePath.set(false)
}

javafx {
    version = '24-ea+5'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

dependencies {
    implementation('eu.hansolo:tilesfx:21.0.9') {
        exclude(group: 'org.openjfx')
    }
    implementation 'lk.vivoxalabs.customstage:CustomStage:1.3.2'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.jetbrains:annotations:24.0.0'

    // Tester
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // To process DB
    implementation 'org.xerial:sqlite-jdbc:3.46.1.3'
    implementation 'org.duckdb:duckdb_jdbc:1.1.1'

    // Encrypt password
    implementation 'com.password4j:password4j:1.8.2'
}


test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}